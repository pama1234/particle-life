buildscript{
  dependencies {
    classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessPluginVersion"

    // This follows advice from https://blog.gradle.org/log4j-vulnerability
    constraints {
      classpath("org.apache.logging.log4j:log4j-core") {
        version {
          strictly("[2.17, 3[")
          prefer("2.17.0")
        }
        because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: Log4j vulnerable to remote code execution and other critical security vulnerabilities")
      }
    }
  }
}

plugins {
    id 'java'
    id 'java-library'  // need this for "api" dependencies
    id 'maven-publish'
}

group 'com.particle.life'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    api 'org.joml:joml:1.10.1'  // is exposed, e.g. by com.particle_life.Accelerator
}

publishing {
    publications {
        particleLife(MavenPublication) {
            artifactId 'particle-life'
            from components.java
        }
    }
}

apply plugin: "com.diffplug.spotless"

spotless {
    lineEndings 'UNIX'

    java {
        target '**/*.java'
        removeUnusedImports()
        eclipse().configFile new File(rootProject.projectDir.absolutePath, 'doc/eclipse.formatter.xml')
    }
    groovyGradle {
        target '*.gradle'
        greclipse().configFile new File(rootProject.projectDir.absolutePath, 'doc/eclipse.formatter.xml')
    }
}